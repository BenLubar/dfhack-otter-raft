module Update-DF-Structures<$VersionNumber>
{
    for server LinuxBuilder
    {
        for directory $WorkingDirectory/df-structures-update
        {
            set $NormalizedVersion = $Replace($VersionNumber, .0, .);

            foreach $OSArch in @(win32, win64, linux32, linux64, osx32, osx64)
            {
                call DFHack-Universal-Package
                (
                    GroupName: dwarffortress/core,
                    PackageName: $OSArch,
                    NormalizedVersion: $NormalizedVersion,
                    OutputPath: $WorkingDirectory/$OSArch,
                    FeedName: DwarfFortress
                );
            }

            call DFHack-Git
            (
                RepoID: DFHack-df-structures,
                Repository: https://github.com/DFHack/df-structures.git,
                Directory: $WorkingDirectory/df-structures,
                Branch: master
            );

            call DFHack-Git
            (
                RepoID: jjyg-df_misc,
                Repository: https://github.com/jjyg/df_misc.git,
                Directory: $WorkingDirectory/df_misc,
                Branch: master
            );

            call DFHack-Git
            (
                RepoID: jjyg-metasm,
                Repository: https://github.com/jjyg/metasm.git,
                Directory: $WorkingDirectory/metasm,
                Branch: master
            );

            SHCall dfhack::dfhack-structures-update
            (
                Arguments: '"$WorkingDirectory" "$VersionNumber"',
                Verbose: true,
                ErrorOutputLogLevel: Debug
            );
        }
    }

    for server localhost
    {
        GitHub-GetSource
        (
            Credentials: GitHub-BenLubar,
            Organization: BenLubar,
            Repository: git-lfs-example-repository,
            DiskPath: $WorkingDirectory/dummy
        );

        Post-Http https://api.github.com/repos/DFHack/df-structures/pulls
        (
            ContentType: application/json,
            TextData: $ToJson(%(title: "Automated df-structures update for DF $VersionNumber", head: "BenLubar:auto-symbols-update", base: master, body: "This is an automated pull request. Only one such pull request should be made for each Dwarf Fortress release. If this bot is malfunctioning, contact BenLubar on IRC.", maintainer_can_modify: true)),
            LogRequestData: false,
            LogResponseBody: true,
            ProxyRequest: false,
            UserName: $GetCredentialProperty(GitHub-BenLubar, UserName),
            Password: $GetCredentialProperty(GitHub-BenLubar, Password)
        );
    }
}

for server GODZILLA
{
    Ensure-Directory $WorkingDirectory;

    Log-Debug Last ETag: $LastDFReleaseETag;

    Log-Debug Last Modified: $LastDFReleaseModified;

    PSCall dfhack::check-df-version
    (
        workingDirectory: $WorkingDirectory,
        lastETag: $LastDFReleaseETag,
        lastModified: $LastDFReleaseModified,
        etag => $NewETag,
        modified => $NewModified,
        version => $VersionNumber,
        date => $Date,
        versions => $VersionFilesJson
    );

    Log-Debug ETag: $NewETag;

    Log-Debug Modified: $NewModified;

    Log-Debug Version: $VersionNumber;

    Log-Debug Date: $Date;

    set @VersionFiles = @FromJson($VersionFilesJson);

    foreach $FileName in @VersionFiles
    {
        # File
        {
            call DF-Handle-New-Version
            (
                FileName: $FileName,
                VersionNumber: $VersionNumber
            );
        }
    }

    if $ListCount(@VersionFiles) != 0
    {
        call Update-DF-Structures
        (
            VersionNumber: $VersionNumber
        );
    }

    Otter::Set-Variable
    (
        Credentials: Otter-Variables,
        Name: LastDFReleaseETag,
        Value: $NewETag
    );

    Otter::Set-Variable
    (
        Credentials: Otter-Variables,
        Name: LastDFReleaseModified,
        Value: $NewModified
    );
}

##AH:EvaluateStatementDescriptions


set $GitHubBuildContext = "";

if $IsVariableDefined(OverrideStructuresCommit)
{
    set $GitHubBuildContext = auto-structures;
}
else
{
    if $PluginTag != ""
    {
        Set-Execution-Priority 50;

        set $GitHubBuildContext = release;
    }
    else
    {
        if $DFHackTag != ""
        {
            Set-Execution-Priority 10;

            set $GitHubBuildContext = pre-$DFHackTag;
        }
        else
        {
            Set-Execution-Priority 30;

            set $GitHubBuildContext = develop;
        }
    }
}

if $GetVariableValue(TriggeredByDFHack)
{
    Set-Execution-Priority 5;
}

try
{
    GitHub-Set-Status
    (
        Credentials: GitHub-BenLubar,
        Organization: $PluginOwner,
        Repository: $ApplicationName,
        AdditionalContext: $GitHubBuildContext,
        CommitHash: $PluginCommit,
        Description: "#$ExecutionId downloading source code...",
        Status: pending
    );

    # Get Source
    {
        # Git
        with lock = !DFHackGit
        {
            foreach server in @ServersInRole(DFHack-Linux-Builders)
            {
                # Linux ($ServerName)
                with async = Git
                {
                    call global::DFHack-Plugin-Get-Sources();
                }
            }

            await Git;
        }

        for server $ListItem(@ServersInRole(DFHack-Linux-Builders), 0)
        {
            if $FileExists($WorkingDirectory/src/plugins/$ApplicationName/test/main.lua)
            {
                Create-Artifact test-scripts
                (
                    From: $WorkingDirectory/src/plugins/$ApplicationName/test,
                    Verbose: true,
                    Overwrite: true
                );
            }
        }

        GitHub-Set-Status
        (
            Credentials: GitHub-BenLubar,
            Organization: $PluginOwner,
            Repository: $ApplicationName,
            AdditionalContext: $GitHubBuildContext,
            CommitHash: $PluginCommit,
            Description: "#$ExecutionId downloading dependencies...",
            Status: pending
        );

        with lock = !DFHackDependencies
        {
            # Linux
            foreach server in @ServersInRole(DFHack-Linux-Builders)
            {
                # Linux Dependencies ($ServerName)
                {
                    with async = Dependencies
                    {
                        Create-File $WorkingDirectory/src/plugins/CMakeLists.custom.txt
                        (
                            Text: "add_subdirectory($ApplicationName)",
                            Overwrite: true
                        );
                    }

                    with async = Dependencies
                    {
                        call global::DFHack-Cache-Dependencies
                        (
                            DependencyPath: $WorkingDirectory/src/CMake/downloads
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:latest,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:gcc-4.8,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:msvc,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /bin/bash,
                            Arguments: "-c 'mkdir -p `$HOME/.ccache {out,build}{32,64}{{,-osx}{,-4.8},-win}'",
                            WorkingDirectory: $WorkingDirectory
                        );
                    }
                }
            }

            await Dependencies;
        }
    }

    GitHub-Set-Status
    (
        Credentials: GitHub-BenLubar,
        Organization: $PluginOwner,
        Repository: $ApplicationName,
        AdditionalContext: $GitHubBuildContext,
        CommitHash: $PluginCommit,
        Description: "#$ExecutionId waiting to build...",
        Status: pending
    );

    if $PluginTagged
    {
        call global::DFHack-Plugin-Build
        (
            BuildType: Release,
            GitHubBuildContext: $GitHubBuildContext
        );
    }
    else
    {
        call global::DFHack-Plugin-Build
        (
            BuildType: RelWithDebInfo,
            GitHubBuildContext: $GitHubBuildContext
        );
    }
}
catch
{
    error;
}

GitHub-Set-Status
(
    Credentials: GitHub-BenLubar,
    Organization: $PluginOwner,
    Repository: $ApplicationName,
    AdditionalContext: $GitHubBuildContext,
    CommitHash: $PluginCommit,
    Status: auto
);

##AH:EvaluateStatementDescriptions

with lock = !DFHackTriggerPlugins
{
    set $DFHackVersion = $ReleaseNumber;

    if $DFHackTag != ""
    {
        set $DFHackVersion = dfhack-$ReleaseNumber;
    }

    foreach $PluginName in @ApplicationsInGroup(DFHack Plugins)
    {
        try
        {
            Create-Release
            (
                Application: $PluginName,
                ReleaseNumber: $DFHackVersion,
                Pipeline: DFHack-Plugin,
                ReleaseName: $ReleaseName,
                Variables: %(DFHackBranch: $DFHackBranch, DFHackTag: $DFHackTag, PluginTagged: false)
            );
        }
        catch
        {
        }
    }
}

set $GitHubBuildContext = develop;

set $GitHubBuildCommit = $DFHackCommit;

set $GitHubBuildRepository = dfhack;

if $IsVariableDefined(OverrideStructuresCommit)
{
    Set-Execution-Priority 60;

    set $GitHubBuildContext = auto-structures;

    set $GitHubBuildCommit = $OverrideStructuresCommit;

    set $GitHubBuildRepository = df-structures;
}
else
{
    if $DFHackTag != ""
    {
        set $GitHubBuildContext = release;

        Set-Execution-Priority 70;
    }
    else
    {
        Set-Execution-Priority 40;
    }
}

try
{
    GitHub-Set-Status
    (
        Credentials: GitHub-DFHack,
        Organization: DFHack,
        Repository: $GitHubBuildRepository,
        AdditionalContext: $GitHubBuildContext,
        CommitHash: $GitHubBuildCommit,
        Description: "#$ExecutionId downloading source code...",
        Status: pending
    );

    # Get Source
    {
        Wait-For-Higher-Priority-Executions
        (
            Verbose: true
        );

        # Git
        with lock = !DFHackGit
        {
            if false
            {
                foreach server in @ServersInRole(DFHack-Windows-Builders)
                {
                    # Windows ($ServerName)
                    with async = Git
                    {
                        call DFHack-Get-Sources();
                    }
                }
            }

            foreach server in @ServersInRole(DFHack-Linux-Builders)
            {
                # Linux ($ServerName)
                with async = Git
                {
                    call DFHack-Get-Sources();
                }
            }

            await Git;
        }

        GitHub-Set-Status
        (
            Credentials: GitHub-DFHack,
            Organization: DFHack,
            Repository: $GitHubBuildRepository,
            AdditionalContext: $GitHubBuildContext,
            CommitHash: $GitHubBuildCommit,
            Description: "#$ExecutionId downloading dependencies...",
            Status: pending
        );

        Wait-For-Higher-Priority-Executions
        (
            Verbose: true
        );

        with lock = !DFHackDependencies
        {
            if false
            {
                # Windows
                foreach server in @ServersInRole(DFHack-Windows-Builders)
                {
                    # Windows Dependencies ($ServerName)
                    {
                        with async = Dependencies
                        {
                            call DFHack-Cache-Dependencies
                            (
                                DependencyPath: $WorkingDirectory/src/CMake/downloads
                            );
                        }

                        with async = Dependencies
                        {
                            Execute-PowerShell >>Set-Location $WorkingDirectory
NuGet.exe install -NonInteractive -Source https://api.nuget.org/v3/index.json clcache
Move-Item .\clcache.*\clcache-*\ .\clcache
Remove-Item -Recurse .\clcache.*\>>;

                            Create-File $WorkingDirectory\clcache.bat
                            (
                                Text: >>`@echo off
`@setlocal
set CLCACHE_DIR=$BaseWorkingDirectory\dfhack-clcache
set CLCACHE_BASEDIR=$WorkingDirectory
"$WorkingDirectory\clcache\clcache.exe" `%*
>>
                            );
                        }
                    }
                }
            }

            # Linux
            foreach server in @ServersInRole(DFHack-Linux-Builders)
            {
                # Linux Dependencies ($ServerName)
                {
                    with async = Dependencies
                    {
                        call DFHack-Cache-Dependencies
                        (
                            DependencyPath: $WorkingDirectory/src/CMake/downloads
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:latest,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:gcc-4.8,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /usr/bin/docker,
                            Arguments: pull $DFHackBuildEnv:msvc,
                            ErrorOutputLogLevel: Debug
                        );
                    }

                    with async = Dependencies
                    {
                        InedoCore::Exec
                        (
                            FileName: /bin/bash,
                            Arguments: "-c 'mkdir -p `$HOME/.ccache {out,build}{32,64}{{,-osx}{,-4.8},-win}{,-dbg}'",
                            WorkingDirectory: $WorkingDirectory
                        );

                        call DFHack-Cache-OSX
                        (
                            TargetPath: $WorkingDirectory/out32-osx,
                            Bits: 32
                        );

                        call DFHack-Cache-OSX
                        (
                            TargetPath: $WorkingDirectory/out64-osx,
                            Bits: 64
                        );

                        call DFHack-Cache-OSX
                        (
                            TargetPath: $WorkingDirectory/out32-osx-4.8,
                            Bits: 32
                        );

                        call DFHack-Cache-OSX
                        (
                            TargetPath: $WorkingDirectory/out64-osx-4.8,
                            Bits: 64
                        );

                        if $DFHackTagged
                        {
                            call DFHack-Cache-OSX
                            (
                                TargetPath: $WorkingDirectory/out32-osx-dbg,
                                Bits: 32
                            );

                            call DFHack-Cache-OSX
                            (
                                TargetPath: $WorkingDirectory/out64-osx-dbg,
                                Bits: 64
                            );

                            call DFHack-Cache-OSX
                            (
                                TargetPath: $WorkingDirectory/out32-osx-4.8-dbg,
                                Bits: 32
                            );

                            call DFHack-Cache-OSX
                            (
                                TargetPath: $WorkingDirectory/out64-osx-4.8-dbg,
                                Bits: 64
                            );
                        }
                    }
                }
            }

            await Dependencies;
        }
    }

    GitHub-Set-Status
    (
        Credentials: GitHub-DFHack,
        Organization: DFHack,
        Repository: $GitHubBuildRepository,
        AdditionalContext: $GitHubBuildContext,
        CommitHash: $GitHubBuildCommit,
        Description: "#$ExecutionId waiting to build...",
        Status: pending
    );

    Wait-For-Higher-Priority-Executions
    (
        Verbose: true
    );

    if $DFHackTagged
    {
        if !$GetVariableValue(DFHackReleaseDeployed, scalar)
        {
            with lock = !DFHackCoreBuildRelease
            {
                # Build (Release)
                {
                    GitHub-Set-Status
                    (
                        Credentials: GitHub-DFHack,
                        Organization: DFHack,
                        Repository: $GitHubBuildRepository,
                        AdditionalContext: $GitHubBuildContext,
                        CommitHash: $GitHubBuildCommit,
                        Description: "#$ExecutionId building...",
                        Status: pending
                    );

                    call DFHack-Build-Core
                    (
                        BuildType: Release
                    );
                }

                # Publish
                {
                    call Publish-DFHack-Release();
                }

                with lock = !DFHackVariable
                {
                    Set-ReleaseVariable DFHackReleaseDeployed
                    (
                        Value: true,
                        Package: $PackageNumber
                    );
                }
            }
        }

        Set-Execution-Priority 40;

        Wait-For-Higher-Priority-Executions
        (
            Verbose: true
        );

        GitHub-Set-Status
        (
            Credentials: GitHub-DFHack,
            Organization: DFHack,
            Repository: $GitHubBuildRepository,
            AdditionalContext: $GitHubBuildContext,
            CommitHash: $GitHubBuildCommit,
            Description: "#$ExecutionId building in debug mode...",
            Status: pending
        );

        call DFHack-Build-Core
        (
            BuildType: RelWithDebInfo,
            ArtifactSuffix: -dbg
        );
    }
    else
    {
        GitHub-Set-Status
        (
            Credentials: GitHub-DFHack,
            Organization: DFHack,
            Repository: $GitHubBuildRepository,
            AdditionalContext: $GitHubBuildContext,
            CommitHash: $GitHubBuildCommit,
            Description: "#$ExecutionId building...",
            Status: pending
        );

        call DFHack-Build-Core
        (
            BuildType: RelWithDebInfo
        );
    }
}
catch
{
    error;
}

GitHub-Set-Status
(
    Credentials: GitHub-DFHack,
    Organization: DFHack,
    Repository: $GitHubBuildRepository,
    AdditionalContext: $GitHubBuildContext,
    CommitHash: $GitHubBuildCommit,
    Status: auto
);
